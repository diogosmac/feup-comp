options
{
    LOOKAHEAD=1;
    MULTI = true;
}

PARSER_BEGIN(Parser)

public class Parser
{
    /**
     * Current number os syntax errors while parsing
     */
    private static int numErrors = 0;

    /**
     * Maximum number of syntax errors accepted before terminating
     * parsing due to too many errors
     */
    private static int MAX_ERRORS = 10;

    /**
    * Main method
    * @param args
    * @throws ParseException
    */
    public static void main(String[] args) throws ParseException {
        System.out.println("Parser for programs in the Java-- language");

        Parser parser;
        // open file as input stream
        try {
            numErrors = 0;
            parser = new Parser(new java.io.FileInputStream(args[0]));
        }
        catch (java.io.FileNotFoundException e) {
            System.out.println("ERROR: file " + args[0] + " not found.");
            return;
        }

        SimpleNode root;
        // create Syntax Tree (returns reference to root node)
        root = parser.Tree();

        // added
        if(numErrors != 0) {
            throw(new ParseException("Parsing has errors!"));
        }

        // Print tree to console
        root.dump("");
    }

    /**
     * Report syntax error and increment error counter
     */
    static void reportError(ParseException e, String statement) throws ParseException {
        // Print Warning
        System.out.println("Syntax error on " + statement + " statement.");
        // Print parsing error
        System.out.println(e.toString());
        // Print separator
        System.out.println("========================");
        numErrors++;

        if (numErrors >= MAX_ERRORS) {
              throw(new ParseException("Reached maximum number of errors (" + MAX_ERRORS + "). Terminating."));
        }
    }
}

PARSER_END(Parser)

/**
 * Tokens to ignore
 */
SKIP :
{
    < WHITESPACE:      " "                                                         > |
    < TAB:             "\t"                                                        > |
    < CARRIAGE_RETURN: "\r"                                                        > |
    < NEWLINE:         "\n"                                                        > |
    < INLINE_COMMENT:  "//" ( ~[ "\n", "\r" ] )* ( "\n" | "\r" | "\r\n" )          > |
    < BLOCK_COMMENT:   "/*" ( ~[ "*" ] )* "*" ( ~[ "/" ] (~[ "*" ] )* "*")* "/"    >
}

/**
 * Keywords
 */
TOKEN:
{
    < IMPORT:       "import"                         > |
    < EXTENDS:      "extends"                        > |
    < PUBLIC:       "public"                         > |
    < STATIC:       "static"                         > |
    < CLASS:        "class"                          > |
    < RETURN:       "return"                         > |
    < MAIN:         "main"                           > |
    < WHILE:        "while"                          > |
    < IF:           "if"                             > |
    < ELSE:         "else"                           > |
    < NEW:          "new"                            > |
    < THIS:         "this"                           > |
    < LENGTH:       "length"                         > |
    < TRUE:         "true"                           > |
    < FALSE:        "false"                          >
}

/**
 * Variable types
 */
TOKEN:
{
    < VOID:         "void"                           > |
    < INT:          "int"                            > |
    < BOOLEAN:      "boolean"                        > |
    < STRING:       "String"                         >
}

/**
 * Operations
 */
TOKEN:
{
    < ASSIGNMENT:   "="                              > |
    < AND:          "&&"                             > |
    < LT:           "<"                              > |
    < SUM:          "+"                              > |
    < SUB:          "-"                              > |
    < MULT:         "*"                              > |
    < DIV:          "/"                              > |
    < NOT:          "!"                              >
}

/**
 * Code characters
 */
TOKEN:
{
    < COMMA:        ","                              > |
    < DOT:          "."                              > |
    < SEMI_COLON:   ";"                              > |
    < OPEN_BLOCK:   "{"                              > |
    < CLOSE_BLOCK:  "}"                              > |
    < OPEN_ARRAY:   "["                              > |
    < CLOSE_ARRAY:  "]"                              > |
    < OPEN_PAR:     "("                              > |
    < CLOSE_PAR:    ")"                              >
}

/**
 * Integers and identifiers
 */
TOKEN:
{
    < INTEGER:      <BINARY> | <OCTAL> | <DECIMAL> | <HEXADECIMAL>      > |
    < #BINARY:      ( "0b" | "0B" ) ( [ "0"-"1" ] )+                    > |
    < #OCTAL:       "0" ( [ "0"-"7" ] )*                                > |
    < #DECIMAL:     [ "1"-"9" ] ( [ "0"-"9" ] )*                        > |
    < #HEXADECIMAL: ( "0x" | "0X" ) ( [ "0"-"9", "A"-"F", "a"-"f" ] )+  > |

    < IDENTIFIER:   <LETTER> ( <LETTER> | <DIGIT> )*                    > |
    < #LETTER:      [ "$", "_", "a"-"z", "A"-"Z" ]                      > |
    < #DIGIT:       [ "0"-"9" ]                                         >
}


SimpleNode Tree(): {}
{
    Program(1) { return jjtThis; } // Java code inside brackets
}


void Program(int sign): {}
{
    ImportDeclaration()
    ClassDeclaration()
    <EOF>
}


void ImportDeclaration(): {} {
    ( <IMPORT> [ <STATIC> ] <IDENTIFIER> [ ImportMethod() ] <SEMI_COLON> )*
}

void ImportMethod(): {} {
    <DOT> <IDENTIFIER> <OPEN_PAR>
        [ <VOID> | Type() ( <COMMA> Type() )* ] // Nothing | VOID | Types
    <CLOSE_PAR> [ <VOID> | Type() ]
}


void ClassDeclaration(): {}
{
    <CLASS> <IDENTIFIER> [ <EXTENDS> <IDENTIFIER> ] <OPEN_BLOCK>
        ( VarDeclaration() )*
        ( MethodDeclaration() )*
    <CLOSE_BLOCK>
}


void VarDeclaration(): {}
{
    Type() <IDENTIFIER> <SEMI_COLON>
}


void MethodDeclaration(): {} { <PUBLIC> ( RegularMethod() | MainMethod() ) }

void RegularMethod() #void: {} {
    Type() <IDENTIFIER> <OPEN_PAR> [ MethodParams() ] <CLOSE_PAR>
    <OPEN_BLOCK>
        MethodBody()
        <RETURN> Expression() <SEMI_COLON>
    <CLOSE_BLOCK>
}
void MethodParams() #void: {} { Type() <IDENTIFIER> ( <COMMA> Type() <IDENTIFIER> )* }

void MainMethod() #void: {} {
    <STATIC> <VOID> <MAIN> <OPEN_PAR> MainParams() <CLOSE_PAR>
    <OPEN_BLOCK>
        MethodBody()
    <CLOSE_BLOCK>
}
void MainParams() #void: {} { <STRING> <OPEN_ARRAY> <CLOSE_ARRAY> <IDENTIFIER> }

void MethodBody() #void: {} { ( LOOKAHEAD(2) VarDeclaration() )* ( Statement() )* }


void Type(): {}
{
    <INT> [ <OPEN_ARRAY> <CLOSE_ARRAY> ]    |
    <BOOLEAN>                               |
    <IDENTIFIER>
}


void Statement(): {}
{
    MultiStatementBlock()                               |
    IfElseBlock()                                       |
    WhileBlock()                                        |
    <IDENTIFIER> (
        Assignment()                            |
        LOOKAHEAD(2) ArrayAssignment()          |
        ComplexExp() <SEMI_COLON>
    )                                                   |
    StatementExpression() <SEMI_COLON>
}

void MultiStatementBlock() #void: {} { <OPEN_BLOCK> ( Statement() )* <CLOSE_BLOCK> }
void IfElseBlock() #void: {}
{
    try {
        <IF> <OPEN_PAR> Expression() <CLOSE_PAR> Statement() <ELSE> Statement()
    }
    catch(ParseException e) {
        reportError(e, "if");
    }
}
void WhileBlock() #void: {}
{
    <WHILE>
    try {
        <OPEN_PAR> Expression() <CLOSE_PAR>
    }
    catch(ParseException e) {
        reportError(e, "while");
        // DEEP ERROR RECOVERY
        // The loop consumes tokens all the way up to a token of
        // "kind".  We use a do-while loop rather than a while because the
        // current token is the one immediately before the erroneous token
        // (in our case the token immediately before what should have been
        // "while".
        Token t;
        do {
            // getToken(int) does not consume token
            t = getToken(1);
            if(t.kind == EOF || t.kind == OPEN_BLOCK || t.kind == IF || t.kind == WHILE)
                break;
            t = getNextToken();
        } while (t.kind != CLOSE_PAR);
    }
    Statement()
}

void Assignment() #void: {} { <ASSIGNMENT> Expression() <SEMI_COLON> }
void ArrayAssignment() #void: {} {<OPEN_ARRAY> Expression() <CLOSE_ARRAY> <ASSIGNMENT> Expression() <SEMI_COLON> }


void Expression(): {} {
    ( SimpleExp() | <IDENTIFIER> ) ComplexExp()
}

void StatementExpression(): {} {
    SimpleExp() ComplexExp()
}

void ComplexExp() #void: {} {
    [ LOOKAHEAD(2) ExpLink() ComplexExp() ]
}

void And() #void: {} {
    LT() ( LOOKAHEAD(2) <AND> LT() #and(2) )*
}

void LT() #void: {} {
    Sum() ( LOOKAHEAD(2) <LT> Sum() #lt(2) )*
}

void Sum() #void: {} {
    Sub() ( LOOKAHEAD(2) <SUM> Sub() #sum(2) )*
}

void Sub() #void: {} {
    Mult() ( LOOKAHEAD(2) <SUB> Mult() #sub(2) )*
}

void Mult() #void: {} {
    Div() ( LOOKAHEAD(2) <MULT> Div() #mult(2) )*
}

void Div() #void: {} {
    Expression() ( LOOKAHEAD(2) <DIV> Expression() #div(2) )*
}

void ExpLink() #void: {}
{
    CompareOp() Expression()                |
    <OPEN_ARRAY> Expression() <CLOSE_ARRAY> |
    <DOT> ( GetLength() | CallMethod() )
}

void CompareOp() #void: {}
{
    <AND>  |
    <LT>   |
    <SUM>  |
    <SUB>  |
    <MULT> |
    <DIV>
}

void GetLength() #void: {} { <LENGTH> }
void CallMethod() #void: {} {
    <IDENTIFIER>
    try {
        <OPEN_PAR> [ MethodArgs() ] <CLOSE_PAR>
    } catch(ParseException e) {
        // clean parenthesis
        Token t;
        do {
            t = getToken(1);
            if(t.kind == EOF)
                break;
            t = getNextToken();
        } while (t.kind != CLOSE_PAR);
        // throw error so 'while' can report it
        throw(e);
    }
}
void MethodArgs() #void: {} { Expression() ( <COMMA> Expression() )* }


void SimpleExp() #void: {}
{
    <INTEGER>                           |
    <TRUE>                              |
    <FALSE>                             |
    <THIS>                              |
    CreateObject()                      |
    <NOT> Expression()                  |
    try {
        <OPEN_PAR> Expression() <CLOSE_PAR>
    } catch(ParseException e) {
        // clean parenthesis
        Token t;
        do {
            t = getToken(1);
            if(t.kind == EOF)
                break;
            t = getNextToken();
        } while (t.kind != CLOSE_PAR);
        // throw error so 'while' can report it
        throw(e);
    }
}
void CreateObject() #void: {} { <NEW> ( CreateArray() | CreatePrimitive() ) }
void CreateArray() #void: {} { <INT> <OPEN_ARRAY> Expression() <CLOSE_ARRAY> }
void CreatePrimitive() #void: {} { <IDENTIFIER> <OPEN_PAR> <CLOSE_PAR> }
